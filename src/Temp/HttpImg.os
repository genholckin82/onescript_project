&НаКлиенте
Процедура КакСохранитьКартинкуСайтаЧерезHttpЗапросНаКлиенте()
 
    // Запрашивать через http можно не только html-страницы,
    // но и другие объекты, доступные нам через http,
    // например, картинки.
 
    // Сохраним на диск и покажем пользователю картинку, которая
    // размещена в интернете по адресу:
    // thumb7.shutterstock.com/display_pic_with_logo/1014806/270790184/
    // stock-photo-summer-holidays-background-sea-background-270790184.jpg
 
    Соединение = Новый HTTPСоединение("thumb7.shutterstock.com", 80);
 
    // Get-запрос к ресурсу на сервере.
    Запрос = Новый HTTPЗапрос("/display_pic_with_logo/1014806/270790184/" +
        "stock-photo-summer-holidays-background-sea-background-270790184.jpg");
    Результат = Соединение.Получить(Запрос);
 
    Сообщить("Нам вернули код " + Результат.КодСостояния);
 
    // Сама картинка - уже в теле ответа, осталось
    // только записать её в файл на диск в виде двоичных данных.
 
    КаталогДляЭкспериментов = КаталогДокументов() + "\helpme1s.ru";
    СоздатьКаталог(КаталогДляЭкспериментов);
    ФайлКартинки = КаталогДляЭкспериментов + "\test.jpg";
 
    // Записываем картинку на диск.
    Результат.ПолучитьТелоКакДвоичныеДанные().Записать(ФайлКартинки);
 
    Сообщить("Сохранили картинку по адресу " + ФайлКартинки);
 
    // Покажем картинку пользователю.
    ЗапуститьПриложение(ФайлКартинки);    
 
КонецПроцедуры